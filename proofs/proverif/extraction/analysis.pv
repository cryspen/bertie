(*****************************************)
(* Top-level process *)
(*****************************************)
free server_name: bertie__tls13utils__t_Bytes.

free cert_sk: bertie__tls13utils__t_Bytes [private].
(* query attacker(cert_sk). *)

query event(Reachable_simple).

process
    let ha = bertie__tls13crypto__HashAlgorithm_HashAlgorithm_SHA256_c() in
    let ae = bertie__tls13crypto__AeadAlgorithm_AeadAlgorithm_Chacha20Poly1305_c() in
    let v__sa = bertie__tls13crypto__SignatureScheme_SignatureScheme_EcdsaSecp256r1Sha256_c() in
    let v__ks =  bertie__tls13crypto__KemScheme_KemScheme_X25519_c() in
    let algs =  bertie__tls13crypto__Algorithms_c(ha,ae,v__sa,v__ks,false,false) in
    let cert = certificate(spki(v__sa, ecdsa_cert_key_slice), vk_from_sk(cert_sk)) in
    let db = bertie__server__ServerDB_c(
        server_name,
        cert,
        cert_sk,
        None()
    ) in

    new rng_client: impl_CryptoRng___RngCore;
    new rng_server: impl_CryptoRng___RngCore;
    let (rng_client: impl_CryptoRng___RngCore, client_init_out: bitstring) = bertie__tls13handshake__client_init(
         algs,
         server_name,
         None(),
         None(),
         rng_client
       ) in
    let (
             nch: bertie__tls13formats__handshake_data__t_HandshakeData,
             cipher0: Option,
             client_state: bertie__tls13handshake__t_ClientPostClientHello
           ) = client_init_out in

    out(c, nch);
    in(c, nch: bertie__tls13formats__handshake_data__t_HandshakeData);

    let (rng_server: impl_CryptoRng___RngCore, server_init_output: bitstring) = bertie__tls13handshake__server_init(
         algs,
         nch,
         db,
         rng_server
       ) in
    let
      (server_hello: bertie__tls13formats__handshake_data__t_HandshakeData,
       flight: bertie__tls13formats__handshake_data__t_HandshakeData,
       cipher0: Option,
       cipher_hs: bertie__tls13record__t_DuplexCipherStateH,
       cipher1: bertie__tls13record__t_DuplexCipherState1,
       server_state: bertie__tls13handshake__t_ServerPostServerFinished)
    = server_init_output in

    out(c, server_hello);
    out(c, flight);
    in(c, server_hello: bertie__tls13formats__handshake_data__t_HandshakeData);


    let (duplex_cipher_state_h: bitstring,
        client_state: bertie__tls13handshake__t_ClientPostServerHello) = bertie__tls13handshake__client_set_params(
         server_hello,
         client_state
       ) in

    in(c, flight: bertie__tls13formats__handshake_data__t_HandshakeData);

    let (client_finished: bertie__tls13formats__handshake_data__t_HandshakeData,
        cipher: bertie__tls13record__t_DuplexCipherState1,
        client_state: bertie__tls13handshake__t_ClientPostClientFinished) = bertie__tls13handshake__client_finish(
         flight,
         client_state
       ) in

    out(c, client_finished);
    in(c, client_finished: bertie__tls13formats__handshake_data__t_HandshakeData);

    let res = bertie__tls13handshake__server_finish(
         client_finished,
         server_state
       ) in
    event Reachable_simple;  

    0
